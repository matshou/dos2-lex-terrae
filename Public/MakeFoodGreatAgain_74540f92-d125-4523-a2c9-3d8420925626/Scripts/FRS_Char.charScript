INIT
CHARACTER:__Me
FIXEDSTRING: %FRS_Hunger=FRS_Hunger_Str
FIXEDSTRING: %FRS_starv=FRS_Sat_Str
FIXEDSTRING: %FRS_toRest=FRS_toRest_Str
FIXEDSTRING: %FRS_RestOut=FRS_RestOut_Str
FIXEDSTRING: %FRS_thirst=FRS_Thirst_Str
FIXEDSTRING: %FRS_dehydrat=FRS_Dehydrated_Str
EXTERN FLOAT: %FRSHealthIn=1.0
EVENTS

/////////////////////
// Initialization
/////////////////////

EVENT Set_FRS
ON
OnInit()
OnStoryOverride()
ACTIONS
IF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	IF "(c1|c2)&!c3"
		CharacterHasStatus(__Me,LYING)
		CharacterHasStatus(__Me,SITTING)
		CharacterHasStatus(__Me,FRSMed)
	THEN
		StartTimer("Rest0_xTime",30,0)
	ENDIF
	IF "!c1"
		IsTagged(__Me, "UNDEAD")
	THEN
		IF "!c1&!c2&!c3&!c4&!c5"
			CharacterHasStatus(__Me,FRSFull2)
			CharacterHasStatus(__Me,FRSFull)
			CharacterHasStatus(__Me,FRSNoHunger)
			CharacterHasStatus(__Me,FRSHunger)
			CharacterHasStatus(__Me,FRSstarvation)
		THEN
			CharacterApplyStatus(__Me,FRSNoHunger,350,1)
		ELIF "!c1&!c2&!c3&!c4"
			CharacterHasStatus(__Me,FRSquenched)
			CharacterHasStatus(__Me,FRSNoThirst)
			CharacterHasStatus(__Me,FRSThirst)
			CharacterHasStatus(__Me,FRSDehydrated)
		THEN
			CharacterApplyStatus(__Me,FRSNoThirst,250,1)
		ENDIF
	ENDIF
ENDIF

///////////////////////////
// Apply POSITIVE status
///////////////////////////

EVENT FRSFull_1
VARS
ITEM:_Item
//INT: _TurnOut
INT: _FullDuration
INT: _MaxDuration
INT: _MinDuration
INT: _StatusTurns
ON
OnUseItem(__Me,_Item)
ACTIONS
IF "c1&c2&!c3&!c4"
CharacterIsPlayer(__Me)
CharacterIsInParty(__Me)
CharacterIsDead(__Me)
IsTagged(__Me, "UNDEAD")
THEN
	////////////////
	// Using Food
	////////////////
	IF "(c1|c2)&(!c3&!c4)"
		IsTagged(_Item, "FOOD")
		IsTagged(_Item, "INGREDIENT")
		IsTagged(_Item, "POISONED")
		IsTagged(_Item, "VOIDTAINTED")
	THEN
		////////////////
		// Vegetables
		////////////////
		IF "c1&!c2&c3"
			IsTagged(_Item,"VEGETABLE")
			IsTagged(_Item,"Mixed")
			IsTagged(_Item, "FOOD")
		THEN
			Set(_MaxDuration,235)
			Set(_MinDuration,140)
		////////////////
		// Meat & Fish
		////////////////
		ELIF "(c1|c2)&(!c3&c4)"
			IsTagged(_Item,"MEAT")
			IsTagged(_Item,"FISH")
			IsTagged(_Item,"Mixed")
			IsTagged(_Item, "FOOD")
		THEN
			Set(_MaxDuration,300)
			Set(_MinDuration,180)
		///////////////////////////////////
		// Non-Food Ingredients & Herbs
		//////////////////////////////////
		ELIF "(!c1&c2)|c3"
			IsTagged(_Item, "FOOD")
			IsTagged(_Item, "INGREDIENT")
			IsTagged(_Item, "Herb")
		THEN
			Set(_MaxDuration,180)
			Set(_MinDuration,110)
		//////////////////////
		// Food Ingredients
		//////////////////////
		ELIF "c1&c2&!c3&!c4&!c5&!c6"
			IsTagged(_Item, "FOOD")
			IsTagged(_Item, "INGREDIENT")
			IsTagged(_Item,"MEAT")
			IsTagged(_Item,"FISH")
			IsTagged(_Item,"Mixed")
			IsTagged(_Item,"VEGETABLE")
		THEN
			Set(_MaxDuration,205)
			Set(_MinDuration,125)
		////////////////
		// Combo Food
		////////////////
		ELSE
			Set(_MaxDuration,405)
			Set(_MinDuration,245)
		ENDIF
		// Calculation a random duration value
		GetRandomBetween(_FullDuration,_MinDuration,_MaxDuration)

		// Remove existing status effects
		SetFlag(__Me,"updatingHunger")
		CharacterRemoveStatus(__Me,FRSNoHunger)
		CharacterRemoveStatus(__Me,FRSHunger)
		CharacterRemoveStatus(__Me,FRSstarvation)
		IF "c1"
			GetStatusTurns(__Me,FRSFull2,_StatusTurns)
		THEN
			Add(_FullDuration,_StatusTurns)
			Clamp(_FullDuration,0,150)
			CharacterRemoveStatus(__Me,FRSFull2)
			CharacterApplyStatus(__Me,FRSFull2,_FullDuration,1)
		ELSE
			IF "c1"
				GetStatusTurns(__Me,FRSFull,_StatusTurns)
			THEN
				Add(_FullDuration,_StatusTurns)
			ENDIF
			CharacterRemoveStatus(__Me,FRSFull)
			IF "c1"
				IsGreaterThen(_FullDuration,250)
			THEN
				Subtract(_FullDuration,250)
				Clamp(_FullDuration,0,150)
				CharacterApplyStatus(__Me,FRSFull2,_FullDuration,1)
			ELSE
				CharacterApplyStatus(__Me,FRSFull,_FullDuration,1)
			ENDIF
		ENDIF
	////////////////
	// Using Drink
	////////////////
	ELIF "c1&!c2&!c3&!c4"
		IsTagged(_Item, "DRINK")
		IsTagged(_Item, "POISONED")
		IsTagged(_Item, "Potion")
		IsTagged(_Item, "ALCOHOL")
	THEN
		CharacterApplyStatus(__Me,FRSquenched,150,1)
		CharacterRemoveStatus(__Me,FRSDehydrated)
		CharacterRemoveStatus(__Me,FRSNoThirst)
	ENDIF
ENDIF

//////////////////////////
// Apply NEUTRAL status
//////////////////////////

// Apply "no thirst"
EVENT FRSNoThirst_1
ON
OnCharacterStatusRemoved(__Me,FRSquenched)
ACTIONS
IF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	CharacterApplyStatus(__Me,FRSNoThirst,250,1)
ENDIF

// Apply "no hunger"
EVENT FRSNoHunger_1
ON
OnCharacterStatusRemoved(__Me,FRSFull)
ACTIONS
IF "c1"
	HasFlag(__Me,"updatingHunger")
THEN
	ClearFlag(__Me,"updatingHunger")
ELIF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	CharacterApplyStatus(__Me,FRSNoHunger,350,1)
ENDIF

//////////////////////////
// Apply REGULAR hunger
//////////////////////////

EVENT FRSHunger_0
ON
OnCharacterStatusRemoved(__Me,FRSFull2)
ACTIONS
IF "c1"
	HasFlag(__Me,"updatingHunger")
THEN
	ClearFlag(__Me,"updatingHunger")
ELIF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	CharacterApplyStatus(__Me,FRSFull,250,1)
ENDIF

//////////////////////////
// Apply NEGATIVE status
//////////////////////////

// Apply Hunger
EVENT FRSHunger_1
ON
OnCharacterStatusRemoved(__Me,FRSNoHunger)
ACTIONS
IF "c1"
	HasFlag(__Me,"updatingHunger")
THEN
	ClearFlag(__Me,"updatingHunger")
ELIF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	CharacterApplyStatus(__Me,FRSHunger,200,1)
	DisplayText(__Me,%FRS_Hunger,5)
ENDIF

// Apply Starvation
EVENT FRSHunger_2
ON
OnCharacterStatusRemoved(__Me,FRSHunger)
ACTIONS
IF "c1"
	HasFlag(__Me,"updatingHunger")
THEN
	ClearFlag(__Me,"updatingHunger")
ELIF "c1&c2&!c3"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
THEN
	CharacterApplyStatus(__Me,FRSstarvation,-1,1)
	DisplayText(__Me,%FRS_starv,5)
ENDIF

// Apply Thirst
EVENT FRS_Thirst_1
ON
OnCharacterStatusRemoved(__Me,FRSNoThirst)
ACTIONS
IF "c1&c2&!c3&!c4"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterHasStatus(__Me,FRSquenched)
THEN
	CharacterApplyStatus(__Me,FRSThirst,175,1)
	DisplayText(__Me,%FRS_thirst,5)
ENDIF

// Apply Dehydration
EVENT FRS_Thirst_2
ON
OnCharacterStatusRemoved(__Me,FRSThirst)
ACTIONS
IF "c1&c2&!c3&!c4"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterHasStatus(__Me,FRSquenched)
THEN
	CharacterApplyStatus(__Me,FRSDehydrated,-1,1)
	DisplayText(__Me,%FRS_dehydrat,5)
ENDIF

//////////////////
// Rest System
//////////////////

//Use furniture
EVENT toRelax_1
ON
OnCharacterStatusApplied(__Me,SITTING)
OnCharacterStatusApplied(__Me,LYING)
ACTIONS
IF "c1&c2&!c3&!c4"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	IsInCombat(__Me)
THEN
	DisplayText(__Me,%FRS_toRest,5)
	StartTimer("Rest0_xTime",50,0)
	CharacterGetStat(%FRSHealthIn,__Me,Vitality)
ENDIF

//Rest Stop
EVENT FRSRelax_Stop
ON
OnCharacterStatusRemoved(__Me,LYING)
OnCharacterStatusRemoved(__Me,SITTING)
ACTIONS
IF "c1&c2&!c3&!c4"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterHasStatus(__Me,FRSMed)
THEN
	StopTimer("Rest0_xTime")
ELIF "!c1&c2"
	CharacterIsDead(__Me)
	CharacterHasStatus(__Me,FRSMed)
THEN
	CharacterRemoveStatus(__Me,FRSMed)
	CharacterApplyStatus(__Me,FRSRest,600,1)
	StopTimer("Rest0_xTime")
ENDIF

//Meditation
EVENT FRSMeditation_01
ON
OnTimer("Rest0_xTime")
ACTIONS
IF "c1&c2&!c3&!c4&(c5|c6)"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterIsSummon(__Me)
	CharacterHasStatus(__Me,LYING)
	CharacterHasStatus(__Me,SITTING)
THEN
	CharacterApplyStatus(__Me,FRSMed,-1,1)
	CharacterAddSourcePoints(__Me,3)
	IF "c1"
		CharacterHasSkill(__Me, Shout_SpiritVision)
	THEN
		CharacterApplyStatus(__Me,SPIRIT_VISION,-1,1)
	ENDIF
ENDIF

//Spirit-Vision on-off
EVENT FRSSpiritVision_but
ON
OnCharacterStatusAttempt(__Me,SPIRIT_VISION)
ACTIONS
IF "c1&c2&!c3&!c4&(c5|c6)&!c7"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterIsSummon(__Me)
	CharacterHasStatus(__Me,FRSMed)
	CharacterHasStatus(__Me,FRSRest)
	CharacterHasStatus(__Me,SPIRIT_VISION)
THEN
	StartTimer("SV_InXTime",1,0)
ENDIF

EVENT FRSSpiritVision_off
ON
OnSkillCast(__Me,Shout_SpiritVision)
ACTIONS
IF "c1&c2&!c3&!c4&—Å5(c6|c7)&c8"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterIsSummon(__Me)
	CharacterHasSkill(__Me,Shout_SpiritVision)
	CharacterHasStatus(__Me,FRSMed)
	CharacterHasStatus(__Me,FRSRest)
	CharacterHasStatus(__Me,SPIRIT_VISION)
THEN
	CharacterRemoveStatus(__Me,SPIRIT_VISION)
ENDIF

EVENT FRSSpiritVision_OnR
ON
OnTimer("SV_InXTime")
ACTIONS
IF "c1&c2&!c3&!c4&(c5|c6)"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterIsSummon(__Me)
	CharacterHasStatus(__Me,FRSMed)
	CharacterHasStatus(__Me,FRSRest)
THEN
	CharacterApplyStatus(__Me,SPIRIT_VISION,-1,1)
ENDIF

EVENT Spirit_Vision_CansRest
ON
OnCharacterStatusRemoved(__Me,FRSRest)
ACTIONS
IF "c1&!c2"
	CharacterHasSkill(__Me,Shout_SpiritVision)
	CharacterHasStatus(__Me,FRSRest)
THEN
	CharacterRemoveStatus(__Me,SPIRIT_VISION)
ELIF "!c1"
	CharacterHasStatus(__Me,FRSRest)
THEN
	DisplayText(__Me,%FRS_RestOut,4)
ENDIF

EVENT Spirit_Vision_CansMed
ON
OnCharacterStatusRemoved(__Me,FRSMed)
ACTIONS
IF "c1&(c2|c3)"
	CharacterHasSkill(__Me,Shout_SpiritVision)
	CharacterHasStatus(__Me,LYING)
	CharacterHasStatus(__Me,SITTING)
THEN
	CharacterRemoveStatus(__Me,SPIRIT_VISION)
ENDIF

EVENT FRSDamage_Rest_Out
VARS
FLOAT:_FRSHealth
ON
OnCharacterVitalityChanged(__Me,_)
OnTurn(__Me)
ACTIONS
IF "c1&c2&!c3&!c4&!c5&c6"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsDead(__Me)
	CharacterIsSummon(__Me)
	IsTagged(__Me, "UNDEAD")
	CharacterGetStat(_FRSHealth,__Me,Vitality)
THEN
	IF "(c1|c2|c3)&c4"
		CharacterHasStatus(__Me,FRSMed)
		CharacterHasStatus(__Me,LYING)
		CharacterHasStatus(__Me,SITTING)
		IsLessThen(_FRSHealth,%FRSHealthIn)
	THEN
		CharacterRemoveStatus(__Me,FRSMed)
		StopTimer("Rest0_xTime")
	ENDIF
	IF "c1&c2"
		CharacterHasStatus(__Me,FRSRest)
		IsLessThen(_FRSHealth,0.5)
	THEN
		CharacterRemoveStatus(__Me,FRSRest)
	ENDIF
ENDIF

EVENT FRSFrom_dying_1
ON
OnCharacterStatusRemoved(__Me,DYING)
ACTIONS
IF "c1&c2&!c3&!c4"
	CharacterIsPlayer(__Me)
	CharacterIsInParty(__Me)
	CharacterIsSummon(__Me)
	IsTagged(__Me, "UNDEAD")
THEN
	CharacterApplyStatus(__Me,FRSNoHunger,350,1)
	CharacterApplyStatus(__Me,FRSNoThirst,250,1)
ENDIF
